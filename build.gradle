buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    dependencies {
        classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker
task texturePacker {
    doLast {
        TexturePacker.process("${project.rootDir}/sources/atlases", "${project.rootDir}/assets/atlases", "main")
    }
}

allprojects {
    apply plugin: 'idea'
}

configure(subprojects) {
    apply plugin: 'java-library'
    sourceCompatibility = 8.0
    compileJava {
        options.incremental = true
    }

    compileJava.dependsOn texturePacker
}

subprojects {
    version = '0.0.' + getGitHash()
    ext.appName = 'HamballRacers'
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io' }
    }
}
